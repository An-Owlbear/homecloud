set dotenv-load := true
default_test := "\".*\""

run:
	docker run -it --rm \
		-w "/opt/homecloud" \
		-e "air_wd=/opt/homecloud" \
		-e "ENVIRONMENT=DEV" \
		-e "HOMECLOUD_APP_DIR=/Users/george/projects/dissertation/home-server-system/backend" \
		-v $(pwd):/opt/homecloud \
		-v ${HOME}/go/pkg/mod:/go/pkg/mod \
		-v ${HOME}/go/pkg/cache:/root/.cache/go-build \
		-v ${HOST_DOCKER_SOCKET}:/var/run/docker.sock \
		--network=homecloud.app \
		-p 1323:1323 \
		--name homecloud.app-dev \
		--network homecloud-proxy-dev \
		cosmtrek/air \
		-c ".air.toml"

test target=default_test:
	docker run -it --rm \
		-w "/opt/homecloud" \
		-e "TEST_CONTAINER_NAME=homecloud-tester" \
		-v $(pwd):/opt/homecloud \
		-v ${HOME}/go/pkg/mod:/go/pkg/mod \
		-v ${HOME}/go/pkg/cache:/root/.cache/go-build \
		-v ${HOST_DOCKER_SOCKET}:/var/run/docker.sock \
		-v /sys/block:/sys/block \
		-v /tmp/homecloud_testing:/tmp/homecloud_testing \
		--name "homecloud-tester" \
		--entrypoint go \
		golang:1.23 \
		test -p 1 -run {{target}} ./...

launcher:
    go run "./cmd/launcher/main.go"

build_launcher version os arch:
    GOOS={{os}} GOARCH={{arch}} go build -o build/launcher ./cmd/launcher/main.go
    mkdir -p build/ory_config
    cp ory_config/identity.schema.json build/ory_config/
    cp ory_config/invite_code.jsonnet build/ory_config/
    cp -R ory_config/templates build/ory_config/
    cp .env build/.env
    cd build && gtar -cvzf homecloud-{{version}}-{{os}}-{{arch}}.tar.gz ory_config launcher .env --owner=0 --group=0 --no-same-owner --no-same-permissions

build_docker version:
    cd ../ && docker buildx build -t ghcr.io/an-owlbear/homecloud:{{version}} .

deploy_docker_dev ssh_address version:
    mkdir -p build/dev/docker
    docker save -o build/dev/docker/homecloud-docker-dev.tar.gz ghcr.io/an-owlbear/homecloud:{{version}}
    ssh {{ssh_address}} 'mkdir -p /tmp/docker'
    scp build/dev/docker/homecloud-docker-dev.tar.gz {{ssh_address}}:/tmp/docker
    ssh {{ssh_address}} 'docker load -i /tmp/docker/homecloud-docker-dev.tar.gz'

deploy_launcher_dev ssh_address:
    mkdir -p build/dev/ory_config
    GOOS=linux GOARCH=arm64 go build -o build/dev/launcher ./cmd/launcher/main.go
    cp ory_config/identity.schema.json build/dev/ory_config/
    cp ory_config/invite_code.jsonnet build/dev/ory_config/
    cp -R ory_config/templates build/dev/ory_config/
    cp .env build/dev/.env
    cp .dev.env build/dev/.dev.env
    cd build/dev && gtar -cvzf homecloud-dev.tar.gz ory_config launcher .env .dev.env --owner=0 --group=0 --no-same-owner --no-same-permissions
    ssh {{ssh_address}} 'mkdir -p /tmp/homecloud'
    scp build/dev/homecloud-dev.tar.gz {{ssh_address}}:/tmp/homecloud
    ssh -t {{ssh_address}} 'cd /tmp/homecloud; tar -xzf homecloud-dev.tar.gz; sudo ENVIRONMENT=DEV ./launcher;'

deploy_sync_dev ssh_address:
    rsync -a . homecloud@homecloud.local:/tmp/homecloud_backend --exclude '/.git' --filter='dir-merge,- .gitignore'

deploy_test_dev ssh_address test:
    @just deploy_sync_dev {{ssh_address}}
    ssh -t {{ssh_address}} 'cd /tmp/homecloud_backend; sudo just test {{test}}'

deploy_run_dev ssh_address:
    @just deploy_sync_dev {{ssh_address}}
    ssh -t {{ssh_address}} 'cd /tmp/homecloud_backend; sudo just run'

build_deploy ssh_address version:
    @just build_docker {{version}}
    @just deploy_docker_dev {{ssh_address}} {{version}}